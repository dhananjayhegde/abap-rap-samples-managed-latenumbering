managed implementation in class zcl_bp_dh_i_orderheader_m unique;
strict ( 2 );
with draft;

define behavior for ZDH_I_OrderHeader_M alias Order
persistent table zdh_t_order
draft table zdh_t_ordermd
lock master
total etag LocalLastChangedAt
authorization master ( instance )
late numbering
//etag master <field_name>
{
  create;
  update;
  delete;

  field ( readonly ) OrderId, status;

  // Admin fields
  field ( readonly ) LocalCreatedByUser, LocalCreatedAt, LocalLastChangedByUser, LocalLastChangedAt, LastChangedAt;

  field ( features : instance ) TotalAmount, Currency;

  draft action Edit;
  draft action Discard;
  draft action Resume;
  draft action Activate optimized;
  draft determine action Prepare;

  factory action Copy [1];

  determination SetHeaderDefaultValues on modify { create; }
  determination SetItemCurrency on modify { field Currency; }

  determination CollectLateNumberingKeys on save { create; update; }

  side effects
  {
    field Currency affects entity _OrderItem;
  }

  mapping for zdh_t_order corresponding
    {
      OrderId                = order_id;
      description            = description;
      TotalAmount            = total_amount;
      Currency               = currency;
      Status                 = status;
      LocalCreatedByUser     = local_created_by;
      LocalCreatedAt         = local_created_at;
      LocalLastChangedByUser = local_last_changed_by;
      LocalLastChangedAt     = local_last_changed_at;
      LastChangedAt          = last_changed_at;
    }

  association _OrderItem { create; with draft; }

}

define behavior for ZDH_I_OrderItem_M alias OrderItem
persistent table zdh_t_order_item
draft table zdh_t_orderitmmd
lock dependent by _Order
authorization dependent by _Order
late numbering
//etag master <field_name>
{
  update;
  delete;

  field ( readonly ) OrderId, ItemNo;
  field ( readonly ) Status, IsOutline, ParentItemNo;

  // Admin fields
  field ( readonly ) LocalCreatedByUser, LocalCreatedAt, LocalLastChangedByUser, LocalLastChangedAt, LastChangedAt;

  field ( features : instance ) Currency, ItemNoForEdit;

  determination SetItemDefaultValues on modify { create; }
  determination CalculateOrderTotal on modify { field Quantity, NetPrice; }
  determination CalculateOrderTotalOnDelete on modify { delete; }

  side effects
  {
    field Quantity affects entity _Order;
    field NetPrice affects entity _Order;
    $self affects entity _Order;
  }

  mapping for zdh_t_order_item corresponding
    {
      OrderId                = order_id;
      ItemNo                 = item_no;
      ParentItemNo           = parent_item_no;
      IsOutline              = isoutline;
      description            = description;
      Quantity               = quantity;
      OrderUnit              = order_unit;
      NetPrice               = net_price;
      Currency               = currency;
      Status                 = status;
      LocalCreatedByUser     = local_created_by;
      LocalCreatedAt         = local_created_at;
      LocalLastChangedByUser = local_last_changed_by;
      LocalLastChangedAt     = local_last_changed_at;
      LastChangedAt          = last_changed_at;
    }

  association _Order { with draft; }
}